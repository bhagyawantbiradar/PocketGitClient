buildscript {
    ext.buildConfig = [
            'minSdk': 21,
            'targetSdk': 28,
            'compileSdk': 28,

            version: [
                    'major': 1,
                    'minor': 0,
                    'patch': 0,
                    'build': 0
            ]
    ]

    ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
    ext.buildConfig.version['code'] = buildConfig.version.major * 1000000 + buildConfig.version.minor * 10000 + buildConfig.version.patch * 100 + buildConfig.version.build

    ext.versions = [
            'kotlin': '1.2.71',
            'appCompat': '1.0.0',
            'constraintLayout': '1.1.3',
            'junit': '4.12',
            'androidXCore': '1.0.0'
    ]

    ext.deps = [
            'kotlin': [
                    'stdlib': "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
            ],
            'androidx': [
                    'appCompat': "androidx.appcompat:appcompat:${versions.appCompat}",
                    'constraintLayout': "androidx.constraintlayout:constraintlayout:${versions.constraintLayout}",
                    'core': "androidx.core:core-ktx:${versions.androidXCore}",
            ],
            'junit': "junit:junit:${versions.junit}"
    ]

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
